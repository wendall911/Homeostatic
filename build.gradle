plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.71.8'
    id 'me.modmuss50.mod-publish-plugin' version '0.7.3'
}

repositories {
    maven {
        name = "Curse Maven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // Patchouli && Provider
        name = "Blame Jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "Geckolib"
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }
    maven {
        // location of the maven that hosts Mantle and JEI files
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
        content {
            includeGroup "dev.gigaherz.sewingkit"
        }
    }
}

group = 'com.github.wendall911.homeostatic'
version = "${minecraft_version}-${mod_version}"
archivesBaseName = "${mod_name}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

minecraft {
    mappings channel: "${mappings_channel}", version: mappings_version

    runs {
        client {
            workingDirectory project.file("run").canonicalPath
            arg "-mixin.config=${mod_id}.mixins.json"
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                homeostatic {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            arg "-mixin.config=${mod_id}.mixins.json"
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', "${mod_id}",
                    '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/'),
                    '--existing', file('src/generated/resources')

            mods {
                homeostatic {
                    source sourceSets.main
                }
            }
        }

    }    
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    compileOnly fg.deobf("curse.maven:create-328085:4625533")
    compileOnly fg.deobf("curse.maven:sereneseasons-291874:${serene_seasons_version}")
    implementation fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    implementation fg.deobf("xyz.brassgoggledcoders:PatchouliProvider:${patchouli_provider_version}")
    compileOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-${minecraft_version}:${sewingkit_version}")

    if (System.getenv('DATA_GEN') != null) {
        runtimeOnly fg.deobf("curse.maven:create-328085:4625533")
        runtimeOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-${minecraft_version}:${sewingkit_version}")
    }
    else {
        runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
        runtimeOnly fg.deobf("curse.maven:simpletextoverlay-321283:${simpletextoverlay_version}")
        //runtimeOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-${minecraft_version}:${sewingkit_version}")
        //runtimeOnly fg.deobf("curse.maven:immersive_engineering-231951:4412849")
        //runtimeOnly fg.deobf("slimeknights.tconstruct:TConstruct:${minecraft_version}-${tic_version}")
        //runtimeOnly fg.deobf("slimeknights.mantle:Mantle:${minecraft_version}-${mantle_version}")
        //runtimeOnly fg.deobf("curse.maven:create-328085:4625533")
        runtimeOnly fg.deobf("curse.maven:sereneseasons-291874:${serene_seasons_version}")
        //runtimeOnly fg.deobf("curse.maven:primalwinter-393321:4044096")
        //runtimeOnly fg.deobf("curse.maven:crafttweaker-239197:${crafttweaker_version}")
        //runtimeOnly fg.deobf("curse.maven:terrablender-563928:${terrablender_version}")
        //runtimeOnly fg.deobf("curse.maven:byg-247560:${byg_version}")
        //runtimeOnly fg.deobf("curse.maven:bop-220318:3759237")
        //runtimeOnly fg.deobf("curse.maven:botania-225643:3936568")
        //runtimeOnly fg.deobf("curse.maven:curios-309927:3841948")
        //runtimeOnly fg.deobf("curse.maven:croptopia-415438:3947782")
        //runtimeOnly fg.deobf("curse.maven:farmers_delight-398521:3834150")
        //runtimeOnly fg.deobf("curse.maven:xerca-341575:3937763")
        //runtimeOnly fg.deobf("curse.maven:morecraft-59648:3831598")
        //runtimeOnly fg.deobf("curse.maven:sophisticated_core-618298:3960016")
        //runtimeOnly fg.deobf("curse.maven:sophisticated_backpacks-422301:3960033")
        //runtimeOnly fg.deobf("curse.maven:ecologics-570463:3939725")
        //runtimeOnly fg.deobf("curse.maven:geckolib-388172:4181370")
        //runtimeOnly fg.deobf("curse.maven:sprout-565856:3788680")
        //runtimeOnly fg.deobf("curse.maven:arsnouveau-401955:3950802")
        //runtimeOnly fg.deobf("curse.maven:create_cafe-542235:3995985")
        //runtimeOnly fg.deobf("curse.maven:puzzleslib-495476:3804332")
        //runtimeOnly fg.deobf("curse.maven:leavemybarsalone-281715:3671258")
        //runtimeOnly fg.deobf("curse.maven:scuba_gear-399090:3680208")
        //runtimeOnly fg.deobf("curse.maven:kobolds-484967:4019488")
        //runtimeOnly fg.deobf("curse.maven:the_undergarden-379849:3810810")
        //runtimeOnly fg.deobf("curse.maven:twilightforest-227639:4337390")
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": mod_name,
            "Specification-Vendor": "${mod_author}",
            "Specification-Version": "${archiveVersion}",
            "Implementation-Title": "${mod_name}",
            "Implementation-Version": "${archiveVersion}",
            "Implementation-Vendor" :"${mod_author}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta', '*.mixins.json']
def replaceProperties = [
    'version': version, 'forge_mc_version_range': forge_mc_version_range, 'forge_version_range': forge_version_range,
    'license': license, 'mod_id': mod_id, 'mod_author': mod_author, 'mod_description': mod_description,
    'mod_name': mod_name, 'issue_tracker': issue_tracker, 'mod_credits': mod_credits,
    'loader_version_range': loader_version_range, 'mod_update_branch': mod_update_branch
]

processResources {
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    inputs.properties replaceProperties
    exclude '.cache'
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar.finalizedBy('reobfJar', 'signJar')

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

def updates = tasks.register("generateUpdates", Copy) {
    it.outputs.upToDateWhen {
        false
    }

    it.from("./files/templates/") {
        include 'updates.json'
        expand 'version': version
    }

    it.into 'files/'
}

task changelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File('build/libs/' + project.getName() + '-' + version + '-changelog.txt');
    templateContent = file('files/templates/changelog.mustache').getText('UTF-8');
    fromRepo = file("$projectDir");
    toRef = "HEAD";
    fromRef = "${minecraft_version}-${last_mod_version}";
}

tasks.build.dependsOn('changelog', 'generateUpdates')

// Jar Signing
import net.minecraftforge.gradle.common.tasks.SignJar

task signJar(type: SignJar, dependsOn: jar) {
    dependsOn 'reobfJar'

    // Skip the task if our secret data isn't available
    onlyIf {
        project.hasProperty('keyStore')
    }

    keyStore = project.keyStore // This needs to be a path to the keystore file
    alias = project.keyStoreAlias
    storePass = project.keyStorePass
    keyPass = project.keyStoreKeyPass
    inputFile = jar.archiveFile
    outputFile = jar.archiveFile
}

def changelogFile = project(":").projectDir.toString() + '/build/libs/' + project.getName() + '-' + version + '-changelog.txt'
def publishFile = project(":").projectDir.toString() + "/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    if (file(changelogFile).exists()) {
        changelog = file(changelogFile).text
    }
    type = "${release_type}".equals("release") ? STABLE : ("${release_type}".equals("beta") ? BETA : ALPHA)
    modLoaders.addAll(['forge'])

    if (project.hasProperty('hscurseApiKey')) {
        curseforge {
            accessToken = project.hscurseApiKey
            projectId = project.hscurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
            optional('configured')
            optional('catalogue')
            optional('jei')
            optional('simpletextoverlay')
            optional('serene-seasons')
            optional('biomes-o-plenty')
            optional('oh-the-biomes-youll-go')
            optional('croptopia')
            optional('farmers-delight')
            optional('xercamod')
            optional('kitteh6660s-morecraft')
            optional('sophisticated-backpacks')
            optional('ecologics')
            optional('ars-nouveau')
            optional('sprout')
            optional('create-cafe')
            optional('fruit-trees')
            optional('leave-my-bars-alone')
            optional('create')
            optional('patchouli')
            optional('scuba-gear')
            optional('kobolds')
            optional('primal-winter')
            optional('tinkers-construct')
            optional('sewing-kit')
        }
    }
    if (project.hasProperty('hsModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.hsModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
            optional('create')
            optional('ecologics')
            optional('create-cafe')
            optional('scuba-gear')
            optional('farmers-delight')
            optional('xercamod')
            optional('leave-my-bars-alone')
            optional('sprout')
            optional('patchouli')
            optional('jei')
            optional('biomesyougo')
            optional('kobolds')
            optional('primal-winter')
            optional('tinkers-construct')
        }
    }
    if (project.hasProperty('hsGithubToken')) {
    }
}
