plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

version = fabric_mod_version
archivesBaseName = "${mod_id}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_mc_version}:${parchment_version}@zip")
    })

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"
    include "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"
    include group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-fabric', version: "${spectrelib_version}"

    modApi "dev.architectury:architectury-fabric:${architectury_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    modCompileOnly "mezz.jei:jei-1.21-common-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-1.21-fabric:${jei_version}"
    //modCompileOnly "curse.maven:create_fabric-624165:${create_fabric_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"
    // Not available for 1.20.4 :(
    //modCompileOnly "curse.maven:fabric_seasons-413523:${fabric_seasons_version}"
    modCompileOnly "curse.maven:sereneseasons-291874:${serene_seasons_fabric_version}"
    modCompileOnly "curse.maven:glitchcore-955399:${glitch_core_fabric_version}"
    modImplementation "vazkii.patchouli:Patchouli:${patchouli_version}-FABRIC-SNAPSHOT"
    modRuntimeOnly "net.jodah:typetools:0.6.3"
    modImplementation "com.electronwill.night-config:core:3.6.7"

    modRuntimeOnly("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive = false
    }

    if (System.getenv('DATA_GEN') != null) {
        // Datagen mods
    }
    else {
        // Runtime testing mods
        // This doesn't work for whatever reason. Such a strange port
        //modRuntimeOnly "curse.maven:create_fabric-624165:${create_fabric_version}"
        //modRuntimeOnly "curse.maven:biomesoplenty-220318:${bop_fabric_version}"
        //modRuntimeOnly "curse.maven:terrablender-565956:${terrablender_fabric_version}"
        //modRuntimeOnly "curse.maven:simpletextoverlay-321283:${simpletextoverlay_fabric_version}"
        //modRuntimeOnly "curse.maven:more_food-874343:${more_food_fabric_version}"
        modRuntimeOnly "curse.maven:colorful_hearts-854213:${colorful_hearts_fabric_version}"
        //modRuntimeOnly "curse.maven:puzzles_lib-495476:${puzzles_lib_fabric_version}"
        //modRuntimeOnly "curse.maven:forge_config_api_port-547434:${forge_config_api_fabric_version}"
        //modRuntimeOnly "curse.maven:lmba-281715:${lmba_fabric_version}"
    }
}

loom {
    def aw = project(":Common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        commonDatagen {
            client()
            vmArg "-D${mod_id}.common_datagen=1"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../Common/src/generated/resources")}"
        }
        fabricDatagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

processResources {
    // Added so we have book data and damage types built in Forge datagen
    from ('../NeoForge/src/generated/resources/') {
        exclude '**/recipes/*'
        exclude '**/recipe/*'
    }
}

def changelogFile = project(":").projectDir.toString() + '/build/libs/changelog.txt'
def publishFile = project(":").projectDir.toString() + "/Fabric/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    changelog = file(changelogFile).text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add('fabric')

    if (project.hasProperty('hscurseApiKey')) {
        curseforge {
            accessToken = project.hscurseApiKey
            projectId = project.hscurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
			requires('fabric-api')
			optional('jei')
			optional('simpletextoverlay')
			optional('serene-seasons')
			optional('biomes-o-plenty')
			optional('oh-the-biomes-youll-go')
			optional('croptopia')
			optional('farmers-delight-refabricated')
			optional('xercamod')
			optional('kitteh6660s-morecraft')
			optional('sophisticated-backpacks')
			optional('ecologics')
			optional('sprout')
			optional('create-cafe')
			optional('fruit-trees')
			optional('leave-my-bars-alone')
			optional('create')
			optional('patchouli')
			optional('primal-winter')
			optional('more-food-xd')
			optional('eternal-starlight')
			optional('terralith')
        }
    }
    if (project.hasProperty('hsModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.hsModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
            requires('fabric-api')
            optional('create')
            optional('ecologics')
            optional('create-cafe')
            optional('scuba-gear')
            optional('farmers-delight-refabricated')
            optional('xercamod')
            optional('leave-my-bars-alone')
            optional('sprout')
            optional('patchouli')
            optional('jei')
            optional('biomesyougo')
            optional('fruitful-fun')
            optional('more-food-xd')
            optional('eternal-starlight')
            optional('terralith')
        }
    }
    if (project.hasProperty('hsGithubToken')) {
    }
}
